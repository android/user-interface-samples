/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    ext.kotlin_version = '1.5.10'
    ext.coroutines_version = '1.5.0'
    ext.ktlint_version = '0.40.0'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.diffplug.spotless'  version '5.7.0'
}

subprojects {
    apply plugin: 'com.diffplug.spotless'
    spotless {
        java {
            target '**/*.java'
            googleJavaFormat('1.7').aosp()
            licenseHeaderFile project.rootProject.file('scripts/copyright')
        }
        kotlin {
            target '**/*.kt'
            ktlint(ktlint_version).userData(['android': 'true'])
            licenseHeaderFile project.rootProject.file('scripts/copyright')
        }
        groovyGradle {
            target '**/*.gradle' // default target of groovyGradle
            // the Groovy Eclipse formatter extends the Java Eclipse formatter,
            // so it formats Java files by default (unless `excludeJava` is used).
            greclipse().configFile(project.rootProject.file('scripts/greclipse.properties'))
            licenseHeaderFile project.rootProject.file('scripts/copyright'), '(buildscript|apply)'
        }
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}
